plugins {
    id 'java'
}

group 'beanvest.scripts.usagegen'

dependencies {
    implementation(project(":beanvest:beanvest"))
    implementation(project(":beanvest:lib:util"))
    implementation(project(":beanvest:lib:apprunner"))
    implementation(libs.gson)
    implementation(libs.bundles.logging)
    implementation(libs.typescriptGenerator)
    testImplementation(libs.bundles.testing)
}


var generatedDir = project.rootDir.toString() + "/generated/"
var sampleDir = project.rootDir.toString() + "/sample/"
var usualJvmArgs = [
        "-Dproject.dir=${project.rootDir}",
        "-Dgenerated.dir=$generatedDir",
        "-Dsample.dir=$sampleDir"]

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.register('generateUsageDoc', JavaExec) {
    dependsOn("generateSampleJournal")
    group = "generation"
    description = "Regenerates usage doc based on generated sample journals"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "beanvest.scripts.usagegen.GenerateUsageDocMain"
    jvmArgs = usualJvmArgs
}
tasks.register('generateUsageDocNative', JavaExec) {
    dependsOn("generateSampleJournal")
    dependsOn(":beanvest:beanvest:nativeCompile")
    group = "generation"
    description = "Regenerates usage doc based on generated sample journals using native build"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "beanvest.scripts.usagegen.GenerateUsageDocMain"
    environment("NATIVE_BIN_PATH", "${project.rootDir}/beanvest/beanvest/build/native/nativeCompile/beanvest")
    jvmArgs = usualJvmArgs
}
tasks.register('generateSampleJournal', JavaExec) {
    group = "generation"
    description = "Regenerates sample journal"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "beanvest.scripts.usagegen.GenerateSampleJournalMain"
    jvmArgs = usualJvmArgs
}
tasks.register('generateSampleReportJson', JavaExec) {
    group = "generation"
    description = "Regenerates sample journal"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "beanvest.scripts.usagegen.GenerateSampleReportJsonMain"
    jvmArgs = usualJvmArgs
}
tasks.register('generateSampleReportJsonNative', JavaExec) {
    dependsOn(":beanvest:beanvest:nativeCompile")
    group = "generation"
    description = "Regenerates sample journal"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "beanvest.scripts.usagegen.GenerateSampleReportJsonMain"
    environment("NATIVE_BIN_PATH", "${project.rootDir}/beanvest/beanvest/build/native/nativeCompile/beanvest")
    jvmArgs = usualJvmArgs
}

tasks.register('generateSampleOptions', JavaExec) {
    group = "generation"
    description = "Regenerates sample options output"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "beanvest.scripts.usagegen.GenerateSampleOptionsMain"
    jvmArgs = usualJvmArgs
}
